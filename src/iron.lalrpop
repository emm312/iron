use std::str::FromStr;
use crate::frontend::nodes::*;

grammar;

RootNodes: Node = {
    FuncDef,
    ExternNode
}

pub Iron: Vec<Node> = {
    RootNodes*
}

Statement: Node = {
    ExprNode,
    VariableDef,
    IfStatement,
    WhileStatement,
    ReturnNode,
    VarAssignNode,
    ExternNode
    // ForLoopStatement
}

Block: Vec<Node> = {
    Statement*
}

NonBoxedExpr: Expr = {
    FullExpr => *<>
}

ExprNode: Node = {
    <expr: FullExpr> ";" => Node::ExprNode(*expr)
}

FullExpr: Box<Expr> = {
    Expr,
    Comparison
}

Expr: Box<Expr> = {
    Expr ExprOp Factor => Box::new(Expr::BiOpNode(<>)),
    Factor
}

Comparison: Box<Expr> = {
    Expr ComparisonOp Expr => Box::new(Expr::ComparisonNode(<>)),
}

ComparisonOp: Comparison = {
    "==" => Comparison::Eq,
    "!=" => Comparison::Neq,
    ">" => Comparison::Gt,
    "<" => Comparison::Lt,
    ">=" => Comparison::Gte,
    "<=" => Comparison::Lte
}

ExprOp: Operation = {
    "+" => Operation::Add,
    "-" => Operation::Sub
}

Factor: Box<Expr> = {
    Factor FactorOp Term => Box::new(Expr::BiOpNode(<>)),
    Term
}

FactorOp: Operation = {
    "*" => Operation::Mul,
    "/" => Operation::Div,
    "%" => Operation::Mod
}

Term: Box<Expr> = {
    Num => Box::new(Expr::Num(<>)),
    ident => Box::new(Expr::Ident(<>)),
    FuncCall,
    string => Box::new(Expr::String(<>)),
    "(" <Expr> ")"
}

Type: Types = {
    "i8" => Types::I8,
    "i16" => Types::I16,
    "i32" => Types::I32,
    "i64" => Types::I64,
    "u8" => Types::U8,
    "u16" => Types::U16,
    "u32" => Types::U32,
    "u64" => Types::U64,
    "String" => Types::STRING,
    "void" => Types::VOID,
    "bool" => Types::I8,
}

FuncDef: Node = {
    "func" <name:ident>  <args:FuncArgs>  "->" <typ:Type> "{" <block:Block> "}" => Node::FuncDefNode(name, args, typ, block)
}

FuncArgs: Vec<(String, Types)> = {
    "(" <mut params: (<ident> ":" <Type> ",")*> <last: (<ident> ":" <Type>)?> ")" => {
        params.extend(last.clone());
        params
    }, 
}

FuncCallArgs: Vec<Expr> = {
    "(" <mut params: (<NonBoxedExpr> ",")*> <last: (<NonBoxedExpr>)?> ")" => {
        params.extend(last.clone());
        params
    }, 
}

FuncCall: Box<Expr> = {
    <name:ident> <args:FuncCallArgs> => Box::new(Expr::FuncCall(name, args))
}

VariableDef: Node = {
    "let" <name: ident> ":" <typ: Type> "=" <expr: NonBoxedExpr> ";" => Node::VarDefNode(name, typ, expr)
}

IfStatement: Node = {
    "if" <expr: NonBoxedExpr> "{" <block: Block> "}" => Node::IfNode(expr, block)
}

WhileStatement: Node = {
    "while" <expr: NonBoxedExpr> "{" <block: Block> "}" => Node::WhileNode(expr, block)
}

ReturnNode: Node = {
    "return" <expr: NonBoxedExpr> ";" => Node::ReturnNode(expr)
}

VarAssignNode: Node = {
    <i: ident> "=" <expr: NonBoxedExpr> ";" => Node::VarAssignNode(i, expr)
}

// ForLoopStatement: Node = {
//     "for" <start: Statement> ";" <expr: NonBoxedExpr> ";" <end: Statement> ";" "{" <block: Block> "}" => Node::ForLoopNode(Box::new(start), expr, Box::new(end), block)
// }

ExternNode: Node = {
    "extrn" <name: ident> <args: FuncArgs> "->" <ret: Type> ";" => Node::ExternNode(name, args, ret)
}

Num: i64 = r"[0-9]+" => i64::from_str(<>).unwrap();
string: String = r#"".*""# => {
    let mut chars = <>.chars();
    chars.next();
    chars.next_back();
    chars.as_str().to_string()
};
ident: String = r"[a-zA-Z][a-zA-Z0-9_-]*" => <>.to_string();